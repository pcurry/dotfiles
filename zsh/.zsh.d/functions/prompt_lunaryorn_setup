# -*- mode: sh; -*-
# Copyright (c) 2014 Sebastian Wiesner <lunaryorn@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

local _prompt_lunaryorn_cwd
local _prompt_lunaryorn_env

function prompt_lunaryorn_cwd {
  local cwd="${PWD/#$HOME/~}"

  # Compute the working directory
  if [[ "$cwd" == (#m)[/~] ]]; then
    _prompt_lunaryorn_cwd="$MATCH"
    unset MATCH
  else
    _prompt_lunaryorn_cwd="${${${(@j:/:M)${(@s:/:)cwd}##.#?}:h}%/}/${cwd:t}"
  fi
}

function prompt_lunaryorn_env {
  # Compute environment information
  _prompt_lunaryorn_env=''
  if [[ -n $VIRTUAL_ENV ]]; then
    _prompt_lunaryorn_env="%F{blue}py%f:%F{yellow}${VIRTUAL_ENV##*/}%f"
  fi
  if [[ -n $_prompt_lunaryorn_env ]]; then
    _prompt_lunaryorn_env=" in %F{blue}${_prompt_lunaryorn_env}%f"
  fi
}

function prompt_lunaryorn_precmd {
  vcs_info 'prompt_lunaryorn'
  prompt_lunaryorn_cwd
  prompt_lunaryorn_env
}

function prompt_lunaryorn_setup {
  setopt local_options
  unsetopt xtrace ksh_arrays
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_lunaryorn_precmd

  # VCS info styling
  zstyle ':vcs_info:*:prompt_lunaryorn:*' check-for-changes true
  zstyle ':vcs_info:*:prompt_lunaryorn:*' use-prompt-escapes true
  zstyle ':vcs_info:*:prompt_lunaryorn:*' get-revision true
  zstyle ':vcs_info:*:prompt_lunaryorn:*' get-bookmarks true
  zstyle ':vcs_info:*:prompt_lunaryorn:*' stagedstr '+'
  zstyle ':vcs_info:*:prompt_lunaryorn:*' unstagedstr '*'
  zstyle ':vcs_info:*:prompt_lunaryorn:*' hgrevformat '%r' # Local revno only
  zstyle ':vcs_info:hg:prompt_lunaryorn:*' branchformat 'r%r'
  zstyle ':vcs_info:*:prompt_lunaryorn:*' formats \
         '%F{cyan}%r%f@%F{blue}%s%f:%F{yellow}%b%f%%B%F{green}%c%f%F{magenta}%u%f%%b'
  zstyle ':vcs_info:*:prompt_lunaryorn:*' actionformats \
         '%F{cyan}%r%f@%F{blue}%s%f:%F{yellow}%b%f%%B%F{red}(%a)%f%F{green}%c%f%F{magenta}%u%f%%b'

  local pwd='%(!.%B%F{red}.%F{cyan})${_prompt_lunaryorn_cwd}%f%b'
  local sep='%(?.%F{green}.%B%F{red})$%f%b '

  PROMPT="${pwd} ${sep}"
  RPROMPT='${vcs_info_msg_0_}${_prompt_lunaryorn_env}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_lunaryorn_setup "$@"
