# -*- mode: sh; -*-
# Copyright (c) 2014 Sebastian Wiesner <lunaryorn@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

function prompt_lunaryorn_cwd {
  local cwd="${PWD/#$HOME/~}"

  # Compute the working directory
  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_lunaryorn_cwd="$MATCH"
    unset MATCH
  else
    _prompt_lunaryorn_cwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_lunaryorn_env {
  # Compute environment information
  _prompt_lunaryorn_env=''
  if [[ -n "$VIRTUAL_ENV" ]]; then
    _prompt_lunaryorn_env="py:${VIRTUAL_ENV##*/}"
  fi
  if [[ -n "${_prompt_lunaryorn_env}" ]]; then
    _prompt_lunaryorn_env=" ${_prompt_lunaryorn_env}"
  fi
}

function prompt_lunaryorn_precmd {
  vcs_info
  prompt_lunaryorn_cwd
  prompt_lunaryorn_env
}

function prompt_lunaryorn_setup {
  setopt local_options
  unsetopt xtrace ksh_arrays
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_lunaryorn_precmd

  local pwd='%(!.%B%F{red}.%F{cyan})${_prompt_lunaryorn_cwd}%f%b'
  local env='%F{blue}${_prompt_lunaryorn_env}%f'
  local exitcode='%(?.. %B%F{red}%?%f%b)'
  local sep='%F{yellow}$%f '

  PROMPT="${pwd}${env}${exitcode} ${sep}"
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_lunaryorn_setup "$@"
