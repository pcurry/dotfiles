# -*- mode: sh; -*-
# Copyright (c) 2014 Sebastian Wiesner <lunaryorn@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

typeset -gA battery_info_

if [[ $OSTYPE == darwin* ]]; then
  battery_info_[backend]='pmset'

  # Take the values out
  local -a pmset
  local -a battery
  pmset=("${(@f)$(pmset -g batt)}")
  battery=("${(s/; /)${(s/	/)${pmset[(r)*InternalBattery*]}}}")
  battery_info_[raw]="${battery[@]}"
  battery_info_[percent]="${${battery[1]}%%%*}"
  if [[ ${battery[3]} != '(no estimate)' ]]; then
    battery_info_[remaining]="${${battery[3]}%% *}"
  fi

  case $battery[2] in
    discharging) battery_info_[state]='discharging';;
    charging) battery_info_[state]='charging';;
    charged) battery_info_[state]='charged';;
    *) battery_info_[state]='unknown'
  esac
else
  battery_info_[backend]='none'
fi

# Arguments
local context="${1:-default}"
local statestr

zstyle -s ":battery_info:${battery_info_[backend]}:${context}" \
       "${battery_info_[state]}str" statestr || statestr="${battery_info_[state]}"

local format
zstyle -s ":battery_info:${backend}:${context}" \
       format format || format='%p%% (%c, %r remaining)'

typeset -g battery_info_msg_

if [[ $battery_info_[backend] != 'none' ]]; then
  zformat -f battery_info_msg_ "${format}" \
          "p:${battery_info_[percent]}" \
          "r:${battery_info_[remaining]}" \
          "R:${+battery_info_[remaining]}" \
          "c:${statestr}"
fi
