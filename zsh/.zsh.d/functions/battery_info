# -*- mode: sh; -*-
# Copyright (c) 2014 Sebastian Wiesner <lunaryorn@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

typeset -gA battery_info_

if [[ $OSTYPE == darwin* ]]; then
  battery_info_[backend]='pmset'

  # Take the values out
  local battery
  battery=("${(s/; /)${${(s/	/)$(pmset -g batt | tail -n1)}[2]}}")
  battery_info_[raw]="${battery[@]}"
  battery_info_[percent]="${${battery[1]}%%%*}"
  battery_info_[remaining]="${${battery[3]}%% *}"
  if [[ ${battery[2]} == 'discharging' ]]; then
    battery_info_[charging]='no'
  else
    battery_info_[charging]='yes'
  fi
else
  battery_info_[backend]='none'
fi

# Arguments
local context="${1:-default}"
local chargingstr

if [[ $battery_info_[charging] == 'yes' ]]; then
  zstyle -s ":battery_info:${battery_info_[backend]}:${context}" \
       chargingstr chargingstr || chargingstr='charging'
else
  zstyle -s ":battery_info:${battery_info_[backend]}:${context}" \
       dischargingstr chargingstr || chargingstr='discharging'
fi

local format
zstyle -s ":battery_info:${backend}:${context}" \
       format format || format='%p%% (%c, %r remaining)'

typeset -g battery_info_msg_

if [[ $battery_info_[backend] != 'none' ]]; then
  zformat -f battery_info_msg_ "${format}" \
          "p:${battery_info_[percent]}" \
          "r:${battery_info_[remaining]}" \
          "c:${chargingstr}"
fi
