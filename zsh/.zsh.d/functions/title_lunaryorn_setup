# -*- mode: sh; -*-
# Copyright (c) 2014 Sebastian Wiesner <lunaryorn@gmail.com>

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

# Setup the terminal title

function title_lunaryorn_base_title {
  vcs_info 'title_lunaryorn'

  local cwd
  local host

  if [[ -z $vcs_info_msg_0_ ]]; then
    cwd="$(shorten_path "${PWD}")"
  else
    local repo_name="${vcs_info_msg_0_}"
    local repo_subdir="/$(shorten_path "${vcs_info_msg_1_:#.}")"
    local ongoing_action="${vcs_info_msg_2_}"
    # Don't print /. for the repo directory of the repo
    cwd="${repo_name}${repo_subdir:#/}${ongoing_action}"
  fi

  if [[ -n $SSH_CONNECTION ]]; then
    host='%n@%M:'
  fi

  echo -n "${host}${cwd}"
}

function title_lunaryorn_precmd {
  set_terminal_title "$(title_lunaryorn_base_title)"
}

function title_lunaryorn_preexec {
  local base_title="$(title_lunaryorn_base_title)"
  set_terminal_title "${base_title} \$ ${2}"
}

function title_lunaryorn_setup {
  emulate -L zsh

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
  autoload -Uz shorten_path
  autoload -Uz set_terminal_title

  # VCS Info in terminal title
  zstyle ':vcs_info:*:title_lunaryorn:*' check-for-changes false
  zstyle ':vcs_info:*:title_lunaryorn:*' get-revision false
  zstyle ':vcs_info:*:title_lunaryorn:*' get-bookmarks false
  zstyle ':vcs_info:*:title_lunaryorn:*' formats '%r' '%S'
  zstyle ':vcs_info:*:title_lunaryorn:*' actionformats '%r' '%S' '(!)'

  add-zsh-hook precmd title_lunaryorn_precmd
  add-zsh-hook preexec title_lunaryorn_preexec
}

title_lunaryorn_setup "$@"
